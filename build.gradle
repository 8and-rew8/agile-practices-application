plugins {
	id 'java'

	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'

	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.5.1'
}

group = 'com.acme.dbo'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	jcenter()
}

sourceSets {
	test {
		java.srcDirs = ["src/test/java/com/acme/dbo/ut"]
		resources.srcDirs = ["src/test/resources/com/acme/dbo/ut"]
	}

	integrationTest {
		java.srcDirs = ["src/test/java/com/acme/dbo/it"]
		resources.srcDirs = ["src/test/resources/com/acme/dbo/it"]
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.2'
	annotationProcessor 'org.projectlombok:lombok:1.18.2'

	implementation 'com.google.code.findbugs:jsr305:1.3.9'
	implementation 'org.springframework.boot:spring-boot-starter:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-logging:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-security:2.3.1.RELEASE'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.apache.derby:derby:10.13.1.1'
	implementation 'org.liquibase:liquibase-core:3.10.0'
	implementation 'org.apache.httpcomponents:httpclient:4.5.12'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
	implementation 'io.springfox:springfox-swagger2:2.9.2'


	testCompileOnly 'org.projectlombok:lombok:1.18.2'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.2'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.0.RELEASE'
	testImplementation 'org.assertj:assertj-core:3.18.1'
	testImplementation 'de.adesso:junit-insights:1.1.0'

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.5.2'


	integrationTestImplementation 'org.seleniumhq.selenium:selenium-java:3.5.1'
	integrationTestImplementation 'io.github.bonigarcia:webdrivermanager:3.8.1'
	integrationTestImplementation 'com.codeborne:selenide:5.4.1'
	integrationTestImplementation 'ru.yandex.qatools.htmlelements:htmlelements-java:1.20.0'
	integrationTestImplementation 'io.cucumber:cucumber-spring:4.7.1'
	integrationTestImplementation 'io.cucumber:cucumber-junit:4.7.1'

	integrationTestRuntimeOnly 'net.masterthought:cucumber-reporting:5.0.0'
	integrationTestRuntimeOnly 'de.monochromata.cucumber:reporting-plugin:3.0.9'
}

test {
	String includedGroups = System.getProperty("groups") ? System.getProperty("groups") : ''
	String excludedGroups = System.getProperty("excludedGroups") ? System.getProperty("excludedGroups") : 'selenium'
	useJUnitPlatform {
		if(includedGroups != '') includeTags includedGroups
		excludeTags excludedGroups
	}

	maxHeapSize '2G'
	maxParallelForks Runtime.runtime.availableProcessors()
	forkEvery 100

	systemProperty 'de.adesso.junitinsights.enabled', 'true'
	systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
	jacoco {
		destinationFile = file("$buildDir/reports/jacoco/jacoco-test.exec")
	}
}

task integrationTest(type: Test) {
	description 'Runs integration tests.'
	group 'verification'
	shouldRunAfter test

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	String includedGroups = System.getProperty("groups") ? System.getProperty("groups") : ''
	String excludedGroups = System.getProperty("excludedGroups") ? System.getProperty("excludedGroups") : 'selenium'
	useJUnitPlatform() {
		if(includedGroups != '') includeTags includedGroups
		excludeTags excludedGroups
	}

	maxHeapSize '2G'
	maxParallelForks Runtime.runtime.availableProcessors()
	forkEvery 100

	systemProperty 'de.adesso.junitinsights.enabled', 'true'
	systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'

	reports {
		html.enabled true
		junitXml.enabled true
	}

	jacoco {
		destinationFile = file("$buildDir/reports/jacoco/jacoco-integrationTest.exec")
	}
}
check.dependsOn integrationTest

jacocoTestReport {
	dependsOn check
	executionData tasks.withType(Test)
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("$buildDir/reports/jacoco/html")
	}
}

pitest {
	junit5PluginVersion = '0.12'
	testSourceSets = [sourceSets.test, sourceSets.integrationTest]

	threads = 4
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
	jvmArgs = ['-Xmx1024m']

	fileExtensionsToFilter.addAll('xml', 'yaml', 'yml')
}
