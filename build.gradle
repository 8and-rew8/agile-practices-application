plugins {
	id 'java'

	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'

	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.6.0'
}

group = 'com.acme.dbo'
version = '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	jcenter()
}

sourceSets {
	test {
		java.srcDirs = ["src/test/java/com/acme/dbo/ut"]
		resources.srcDirs = ["src/test/resources/com/acme/dbo/ut"]
	}

	integrationTest {
		java.srcDirs = ["src/test/java/com/acme/dbo/it"]
		resources.srcDirs = ["src/test/resources/com/acme/dbo/it"]
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.2'
	annotationProcessor 'org.projectlombok:lombok:1.18.2'

	implementation 'com.google.code.findbugs:jsr305:1.3.9'
	implementation 'org.springframework.boot:spring-boot-starter:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-logging:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-security:2.5.4'
	implementation 'jakarta.validation:jakarta.validation-api:2.0.2'
	implementation 'org.apache.derby:derby:10.13.1.1'
	implementation 'org.liquibase:liquibase-core:4.4.3'
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	implementation 'io.springfox:springfox-swagger2:3.0.0'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'

	runtimeOnly 'org.postgresql:postgresql:42.2.18'

	testCompileOnly 'org.projectlombok:lombok:1.18.20'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

	testImplementation "org.testcontainers:testcontainers:1.16.0"
	testImplementation "org.testcontainers:junit-jupiter:1.16.0"
	testImplementation "org.testcontainers:postgresql:1.16.0"

	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.4'
	testImplementation 'de.adesso:junit-insights:1.1.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.0'

	integrationTestImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
	integrationTestImplementation 'io.github.bonigarcia:webdrivermanager:5.0.0'
	integrationTestImplementation 'com.codeborne:selenide:5.24.2'
	integrationTestImplementation 'ru.yandex.qatools.htmlelements:htmlelements-java:1.20.0'
	integrationTestImplementation 'io.cucumber:cucumber-spring:6.11.0'
	integrationTestImplementation 'io.cucumber:cucumber-junit:6.11.0'
	integrationTestImplementation 'io.cucumber:cucumber-java:6.11.0'

	integrationTestRuntimeOnly 'net.masterthought:cucumber-reporting:5.6.0'
}

test {
	String includedGroups = System.getProperty("groups") ? System.getProperty("groups") : ''
	String excludedGroups = System.getProperty("excludedGroups") ? System.getProperty("excludedGroups") : 'nope'
	useJUnitPlatform {
		if(includedGroups != '') includeTags includedGroups
		excludeTags excludedGroups
	}

	maxHeapSize '2G'
	maxParallelForks Runtime.runtime.availableProcessors()
	forkEvery 100

	systemProperty 'de.adesso.junitinsights.enabled', 'true'
	systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
	jacoco {
		destinationFile = file("$buildDir/reports/jacoco/jacoco-test.exec")
	}
}

task integrationTest(type: Test) {
	description 'Runs integration tests.'
	group 'verification'
	shouldRunAfter test

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	String includedGroups = System.getProperty("groups") ? System.getProperty("groups") : ''
	String excludedGroups = System.getProperty("excludedGroups") ? System.getProperty("excludedGroups") : 'selenium | docker'
	useJUnitPlatform() {
		if(includedGroups != '') includeTags includedGroups
		excludeTags excludedGroups
	}

	maxHeapSize '2G'
	maxParallelForks Runtime.runtime.availableProcessors()
	forkEvery 100

	systemProperty 'de.adesso.junitinsights.enabled', 'true'
	systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'

	reports {
		html.enabled true
		junitXml.enabled true
	}

	jacoco {
		destinationFile = file("$buildDir/reports/jacoco/jacoco-integrationTest.exec")
	}
}
check.dependsOn integrationTest

jacocoTestReport {
	dependsOn check
	executionData tasks.withType(Test)
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("$buildDir/reports/jacoco/html")
	}
}

pitest {
	junit5PluginVersion = '0.12'
	testSourceSets = [sourceSets.test] //TODO add sourceSets.integrationTest but Selenuim and Docker tests break PIT

	threads = 4
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
	jvmArgs = ['-Xmx1024m']

	fileExtensionsToFilter.addAll('xml', 'yaml', 'yml')
}
